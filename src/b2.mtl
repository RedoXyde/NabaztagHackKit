// VLISP - Jan 2012 06
// Nabaztag Code cleanup

proto main 0;;

proto earsInit 0;;

#include "lib/var"
#include "lib/util"
#include "lib/cfg"
#include "lib/arp"
#include "lib/udp"
#include "lib/tcp"
#include "lib/dns"
#include "lib/http"
#include "lib/dhcp"
#include "pinnab"
#include "lib/wifi"

//-------------------- RFID
fun playerUrl tag=
  strcatlist confGetServerUrl::"/players/"::(webmac tag)::".jsp"::nil;;

fun eventUrl data=
  strcatlist confGetServerUrl::"/events.jsp"::"?event[data]="::data::nil;;

fun buttonUrl data=
  strcatlist confGetServerUrl::"/button.jsp?"::data::nil;;

fun logUrl=
  strcatlist confGetServerUrl::"/log.jsp"::nil;;

fun _cbdummyhttp http res=
  0
;;

fun evalTrame res=
  let parseResponse httpgetcontent res -> bufferlist in
    if bufferlist != nil then
    (
      for l = bufferlist; l != nil; tl l do
      (
        let hd l -> [code val] in
        if code == 1 then
        (
          earsInit;
          0
        )
        else if code == 2 then
        (
          httprequest "POST" logUrl (strcat "logs=" logs) #_cbdummyhttp HTTP_NORMAL;
          set logs = "";
          0
        )
        else if code == 9 then
        (
          exit //reboot
        )
        // led buffers: short 20-24 loop 25-29; 3Byte: short 30-34 loop 35-39
        else if code >= 20 && code <= 39 then
        (
          let code % 10 -> index in
          let 2 * (code / 30) + 1 -> step in
          let index / 5 -> loop in
          _bufferFill index step loop val;
          0
        )
        // ear buffers: short 40-41, loop 42-43
        else if code >= 40 && code <= 43 then
        (
          let code % 10 -> index in
          let 1 -> step in
          let index / 2 -> loop in
          _bufferFill index+10 step loop val;
          0
        )
      )
    )
;;

fun _cbHttp http res=
  evalTrame res;
  0
;;

//--------- RFID
const RFID_CHECK_FQ = 500;; //ms MIN 50ms
const RFID_STOP_BYTE = 0xFF;;
const RFID_ID_START = "\208\2\26";; // 0xd0 0x02 0x1a
const RFID_ID_FALSE = "\0\0\0\0\0\0\0\0\v";;
const RFID_ID_ERROR = "\69\114\114\111\114\0\0\0\v";; // 0x45 72 72 6f 72 00 00 00

var rfid_last_check = 0;;
var data = "";;

fun checkRFID=
  if ((time_ms - rfid_last_check) > RFID_CHECK_FQ) then
  (
    let rfidGet -> rfid in
    if (rfid != nil) then
    (
      if ((strstr rfid RFID_ID_START 0) == 0) then
      (
        httprequest "PUT" playerUrl rfid "" #_cbHttp HTTP_NORMAL;
        0
      )
      else if ((strstr rfid RFID_ID_FALSE 0) == 0) then
      (
        0
      )
      else
      (
        let strlen rfid -> len in //usually that's 8
        for index = 0; index < len; index + 1 do
        (
          let strget rfid index -> data_byte in
          (
            if (data_byte != RFID_STOP_BYTE) then
              set data = strcat data ctoh data_byte
            else if ((strlen data) > 0) then
            (
              httprequest "POST" eventUrl data "" #_cbHttp HTTP_NORMAL;
              set data = ""
            )
          )
        );
        0
      )
    );
    //all done
    set rfid_last_check = time_ms
  );
0;;

//--------- LED
const LED_CHECK_FQ = 500;; //ms MIN 50ms
var led_last_check = 0;;
fun setLeds=
   //LED_CHECK_FQ
  let (button3 + 1) * 3 -> led_check_fq in
  if ((time_ms - led_last_check) > led_check_fq) then
  (
    for index = 0; index < 5; index + 1 do
    (
      let colorBufferRead index+5 -> loopColor in
      let colorBufferRead index -> color in
      (
        if color == nil then set color = loopColor;
        if color == nil then set color = 0;
        led index color
      )
    );
    set led_last_check = time_ms
  )
;;

//--------- Button
const BUTTON_REBOOT = 30;; //ms MIN 50ms
var button_pressed = 0;;
fun checkButton=
  if (button2 > 0) then
  (
    set button_pressed = button_pressed + 1;
    if button_pressed == 1 then
    (
      httprequest "GET" buttonUrl (itoa time_ms) "" #_cbHttp HTTP_NORMAL;
      0
    ) else if button_pressed == BUTTON_REBOOT then
    (
      buffersFill 0 4 "\0\255\0\255\0\255\255\255"
    )
  )
  //reboot only when button not pressed, otherwise we get into setup mode
  else if button_pressed > BUTTON_REBOOT then
  (
    exit
  )
  else (
    set button_pressed = 0
  )
;;

//--------- EAR
var ears;;
const EAR_INIT = nil;;
const EAR_STOP = 0;;
const EAR_FORWARD = 1;;
const EAR_BACKWARD = -1;;
const EAR_TICKS = 17;;

type EAR=[dir pos shouldPos lastOffset lastTime lastTime2 lastRead];;

fun earRead index=
  let ears.index -> ear in
  let time_ms -> currentTime in
  (
    let motorget index -> currentOffset in
    let (currentOffset - ear.lastOffset) -> diff in
    (
      if diff > 0 then //ear moved
      (
        // update pos
        if ear.pos != EAR_INIT then
        let ear.dir -> direction in
        (
          if direction == 0 then set direction = 1; //manual moved!!
          set ear.pos = (EAR_TICKS + ear.pos + direction * diff) % EAR_TICKS
        );

        // check for reset Reset
        let currentTime - ear.lastTime -> lastDuration in
        let currentTime - ear.lastTime2 -> totalDuration in
        if ear.dir != EAR_STOP && lastDuration > 1000 && totalDuration < 1400 then
        (
           set ear.pos = (EAR_TICKS + (ear.dir - 1) / 2) % EAR_TICKS; // either 0  or  -1
           bufferFill 0 "\100";
           log "reset" {index lastDuration totalDuration ear.dir ear.pos}
        );
        log "moved" {index ear.lastTime ear.lastTime2 ear.lastRead ear.pos ear.shouldPos ear.dir };

        set ear.lastTime2 = ear.lastTime;
        set ear.lastTime = currentTime
      );
      set ear.lastOffset = currentOffset
    );

    // check  & read new value
    if (ear.pos != EAR_INIT) && (ear.pos == ear.shouldPos) && (currentTime - ear.lastRead) > 270 then
    (
      let bufferRead index+10 -> earPos in
      (
        if earPos == nil then set earPos = bufferRead index+10+2;
        if earPos == nil then set earPos = ear.pos;
        set ear.shouldPos = earPos;
        let ear.pos - ear.shouldPos -> posDiff in
        set ear.dir = posDiff / posDiff;
        motorset index ear.dir;
        set ear.lastRead = currentTime

        // log "ear read" {index time_ms earPos ear.pos ear.shouldPos}
      )
    )
  )
;;

fun earsInit=
  let time_ms -> currentT in
  (
    set ears = tabnew nil 2;
    set ears.0 = [dir:EAR_FORWARD pos:EAR_INIT shouldPos:0 lastTime:currentT lastTime2:currentT];
    set ears.1 = [dir:EAR_FORWARD pos:EAR_INIT shouldPos:0 lastTime:currentT lastTime2:currentT];
    motorset 0 1
    motorset 1 1
  );
0;;

//-------------------- LOOP
fun loop=
  wifiRun;
  wifiCheck;

  setLeds;
  //checkRFID;
  checkButton;
  earRead 0;
  // earRead 1;
0;;

//-------------------- MAIN
fun main=
  confInit;
  wifiInit 0;
  buffersInit 14;
  earsInit;

  loopcb #loop; // 20 p. second

  netstart;
  startdnsclient;

  sndVol 0;
  srand time_ms;
0;;
